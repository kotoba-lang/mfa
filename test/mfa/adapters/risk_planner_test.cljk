(ns mfa.adapters.risk-planner-test
  (:require [clojure.test :refer [deftest is]]
            [mfa.adapters.planner :as planner]
            [mfa.adapters.risk-planner :as risk-planner]
            [mfa.model :as m]))

(deftest high-risk-plan-adds-phishing-resistant-factor
  (let [policy (m/policy "m1" #{:totp} {})
        out (planner/plan (risk-planner/risk-planner {:fallbacks [:recovery]})
                          policy
                          "did:web:example.com:alice"
                          {:risk :high}
                          {:evidence-ref "kagi://risk/1"})]
    (is (= [:totp :webauthn]
           (mapv :authn.factor-request/type (:mfa.plan/factor-requests out))))
    (is (= [:recovery] (:mfa.plan/fallbacks out)))
    (is (= "kagi://risk/1" (:mfa.plan/evidence-ref out)))))

(deftest medium-risk-plan-requires-second-factor
  (let [policy (m/policy "m2" #{:password} {})
        out (planner/plan (risk-planner/risk-planner)
                          policy
                          "did:web:example.com:alice"
                          {:risk :medium}
                          {})]
    (is (= [:password :totp]
           (mapv :authn.factor-request/type (:mfa.plan/factor-requests out))))))

(deftest preferences-order-factor-requests-and-policy-fallbacks
  (let [policy (m/policy "m3" #{:totp :touchid}
                         {:preferences [:touchid :totp]
                          :fallbacks [:recovery-code]})
        out (planner/plan (risk-planner/risk-planner)
                          policy
                          "did:web:example.com:alice"
                          {:risk :low}
                          {})]
    (is (= [:touchid :totp]
           (mapv :authn.factor-request/type (:mfa.plan/factor-requests out))))
    (is (= [:recovery-code] (:mfa.plan/fallbacks out)))))
