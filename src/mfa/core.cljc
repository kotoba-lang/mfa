(ns mfa.core
  (:require [clojure.set :as set]
            [mfa.model :as m]))

(def phishing-resistant-factors m/phishing-resistant-factors)

(defn policy-problems [policy]
  (cond-> []
    (empty? (:mfa.policy/required policy))
    (conj {:mfa.problem/code :missing-required-factors})
    (and (:mfa.policy/phishing-resistant? policy)
         (empty? (set/intersection phishing-resistant-factors
                                   (:mfa.policy/required policy))))
    (conj {:mfa.problem/code :missing-phishing-resistant-factor})))

(defn factor-problems [factor]
  (cond-> []
    (nil? (:authn.factor/type factor))
    (conj {:mfa.problem/code :missing-factor-type})
    (not (contains? factor :authn.factor/ok?))
    (conj {:mfa.problem/code :missing-factor-result})))

(defn- valid-policy! [policy]
  (when-let [ps (seq (policy-problems policy))]
    (throw (ex-info "invalid MFA policy" {:mfa/problems ps})))
  policy)

(defn- valid-factors! [factors]
  (when-let [ps (seq (mapcat factor-problems factors))]
    (throw (ex-info "invalid MFA factors" {:mfa/problems ps})))
  factors)

(defn evaluate [policy factors]
  (valid-policy! policy)
  (valid-factors! factors)
  (m/result policy factors))
